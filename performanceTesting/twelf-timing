#!/usr/bin/env tcsh
if (! -f $1) then
  echo "Usage: twelf-timing [FILE]"
  exit 65
else
 set bname = `basename $1 .elf`
 set lname = ${bname}.twelf
  ~/tools/twelf/bin/twelf-server > ${lname}.output <<INPUTFILE
  set Print.depth 0
  loadFile $1
  Timers.show
INPUTFILE
  echo "overhead" >> ${lname}.timing
  grep '^Parsing' ${lname}.output >> ${lname}.timing
  grep '^Reconstruction' ${lname}.output >> ${lname}.timing
  rm ${lname}.output

  set numqueries = $#
  set i = 2
  while ($i <= $numqueries)
    # obtain time for parsing and solving query
    ~/tools/twelf/bin/twelf-server > ${lname}.output <<INPUTFILE
    set Print.depth 0
    loadFile $1
    Timers.reset
    loadFile ${argv[$i]}
    Timers.show
INPUTFILE
    echo `basename ${argv[$i]} .elf` >> ${lname}.timing
    grep '^Parsing' ${lname}.output >> ${lname}.timing
    grep '^Reconstruction' ${lname}.output >> ${lname}.timing
    grep '^Solving' ${lname}.output >> ${lname}.timing
    rm ${lname}.output

    # obtain time for printing solutions
    ~/tools/twelf/bin/twelf-server > ${lname}.output <<INPUTFILE
    loadFile $1
    Timers.reset
    loadFile ${argv[$i]}
    Timers.show
INPUTFILE
    grep '^Printing' ${lname}.output >> ${lname}.timing
    rm ${lname}.output

    @ i = $i + 1
  end

  cat ${lname}.timing
endif
